12 76
META script_type_update
META script_execution_waiting

/// Constants
CONST LIST connectedUsernames = fe3d:server_get_connected_usernames()
CONST INT totalUsers = misc:list_size("connectedUsernames")

/// Non-constants
STR message = ""
INT readyCount = 0
INT livingCount = 0

/// Count ready players
IF _readyStatus[0] IS <true>:
    INCR readyCount 1
IF _readyStatus[1] IS <true>:
    INCR readyCount 1
IF _readyStatus[2] IS <true>:
    INCR readyCount 1
IF _readyStatus[3] IS <true>:
    INCR readyCount 1

/// Count living players
IF _livingStatus[0] IS <true>:
    INCR livingCount 1
IF _livingStatus[1] IS <true>:
    INCR livingCount 1
IF _livingStatus[2] IS <true>:
    INCR livingCount 1
IF _livingStatus[3] IS <true>:
    INCR livingCount 1

/// Check if everyone is ready
IF _round IS 0 AND readyCount MORE 1 AND readyCount IS totalUsers:
    VEC3 position = [0.0 0.0 0.0]
    DEC yaw = 0.0
    INT maxIndex = totalUsers
    INT index = 0
    EDIT _round = 1
    LOOP:
        IF index IS maxIndex:
            BREAK
        ELSE:
            /// Send start message
            EDIT position = _spawnPositions[index]
            EDIT yaw = _spawnYaws[index]
            CAST position STR
            CAST yaw STR
            EDIT message = misc:string_concat("server:start:", position)
            EDIT message = misc:string_concat(message, ":")
            EDIT message = misc:string_concat(message, yaw)
            fe3d:server_send_tcp_message(connectedUsernames[index], message)
            CAST position VEC3
            CAST yaw DEC
            /// Make player alive
            EDIT _livingStatus[index] = <true>
        INCR index 1

/// Check if round is over
IF livingCount IS 1:
    INCR _round 1
    /// Send next message
    CAST _round STR
    EDIT message = misc:string_concat("server:next:", _round)
    fe3d:server_broadcast_tcp_message(message, "")
    CAST _round INT
    /// Make players alive
    EDIT _livingStatus = {<true>, <true>, <true>, <true>}

/// Check if game is over
INT maxKills = misc:list_max("_kills")
IF _round MORE 0:
    IF maxKills IS 1 OR totalUsers IS 1:
        /// Send stop message
        INT winnerIndex = misc:list_index("_kills", maxKills)
        CAST maxKills STR
        EDIT message = misc:string_concat("server:stop:", connectedUsernames[winnerIndex])
        EDIT message = misc:string_concat(message, ":")
        EDIT message = misc:string_concat(message, maxKills)
        fe3d:server_broadcast_tcp_message(message, "")
        fe3d:server_disconnect_clients()
        /// Reset game
        EDIT _kills = {0, 0, 0, 0}
        EDIT _readyStatus = {<false>, <false>, <false>, <false>}
        EDIT _livingStatus = {<false>, <false>, <false>, <false>}
        EDIT _round = 0
